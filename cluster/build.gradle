apply plugin: 'application'

dependencies {
    compile project(':cluster-api')
    compile project(':dto-cache')
    compile project(':execlogic')
    compile project(':cloud-communication-api')
    compile project(':scheduler-api')
    compile project(':worker-node-api')
    compile project(':reducer-node-api')
    compile project(':client-routine-worker-api')
    compile project(':transfer-util')
    compile project(':domain')
}

mainClassName = 'at.enfilo.def.cluster.server.Cluster'

shadowJar {
    exclude 'cluster.yml'
    exclude 'log4j2.xml'
}

remotes {
    defHost {
        host = 'localhost'
        user = 'def'
        password = 'def'
    }
}

// run 'gradle deploy -Phost=<ip>'
task deploy {
    doLast {
        ssh.run {
            settings {
                knownHosts = allowAnyHosts
            }

            String host = project.getProperties().get('host')
            remotes.defHost.host = host

            println 'Try to deploy on ' + remotes.defHost

            session(remotes.defHost) {
                executeScript file('src/main/sh/stop.sh')
                executeScript file('src/main/sh/clean.sh')
                copy {
                    from "$buildDir/libs"
                    from "$buildDir/resources/main"
                    into "$buildDir/cluster/"
                }
                put from: "$buildDir/cluster", into: "."
                executeScript file('src/main/sh/start.sh')
            }
        }
    }
}

task restart {
    doLast {
        ssh.run {
            settings {
                knownHosts = allowAnyHosts
            }

            String host = project.getProperties().get('host')
            remotes.defHost.host = host

            println 'Try to restart on ' + remotes.defHost
            session(remotes.defHost) {
                executeScript file('src/main/sh/stop.sh')
                executeScript file('src/main/sh/start.sh')
            }
        }
    }
}

deploy.dependsOn shadowJar
